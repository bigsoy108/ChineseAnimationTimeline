apply plugin: 'com.android.application'
apply plugin: 'img-optimizer'
//打包时，记得设置true启用
if (isNeedPackage.toBoolean() && isUseBooster.toBoolean()) {
    apply plugin: 'com.didiglobal.booster'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.SY.ChineseAnimationTimeline"
        minSdkVersion 17
        targetSdkVersion build_versions.target_sdk
        versionCode 2
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    signingConfigs {
        if (isNeedPackage.toBoolean()) {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }

        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def appID = properties.getProperty("APP_ID_UMENG")
                if (appID != null) {
                    buildConfigField "String", "APP_ID_UMENG", appID
                } else {
                    buildConfigField "String", "APP_ID_UMENG", '""'
                }
            } else {
                signingConfig signingConfigs.debug
                buildConfigField "String", "APP_ID_UMENG", '""'
            }
        }

        debug {
            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    //room
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'

    //okhttp
    implementation("com.squareup.okhttp3:okhttp:4.8.1")

    //分包
    implementation deps.androidx.multidex

    implementation 'com.alibaba.android:vlayout:1.2.36'
    //下拉刷新
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-header:1.1.5'
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-layout:1.1.5'

    //腾讯的键值对存储mmkv
    implementation 'com.tencent:mmkv:1.0.22'
    //屏幕适配AutoSize
    implementation 'me.jessyan:autosize:1.1.2'

    //预加载占位控件
    implementation 'me.samlss:broccoli:1.0.0'

    implementation 'com.zzhoujay.richtext:richtext:3.0.8'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    //ANR异常捕获
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'



    //tablayout设计
    implementation 'com.android.support:design:28.0.0'

    //textdrawble
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    //Flexlayout
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

}
//自动添加X-Library依赖
apply from: 'x-library.gradle'



